;******************************************************************************
; *
; Filename: KeypadNotes.asm *
; Date: February 14, 2025 *
; Author: Andrew Keller *
; Company: Idaho State University *
; Description: Plays a different note with each button on a keypad *
; Github: https://github.com/andrew1593571/RCET3375.git *
; *
;******************************************************************************
  
#include "p16f883.inc"

; CONFIG1
; __config 0xE0F1
 __CONFIG _CONFIG1, _FOSC_XT & _WDTE_OFF & _PWRTE_OFF & _MCLRE_ON & _CP_OFF & _CPD_OFF & _BOREN_OFF & _IESO_OFF & _FCMEN_OFF & _LVP_OFF
; CONFIG2
; __config 0xFFFF
 __CONFIG _CONFIG2, _BOR4V_BOR40V & _WRT_OFF
 
    ORG H'000'
    GOTO SETUP
    
    ORG H'004'
    RETFIE
    
CBLOCK H'20'
    COUNTA		;ADDRESS 0x20
    COUNTB		;ADDRESS 0x21
    COUNTC		;ADDRESS 0x22
    STOREA		;ADDRESS 0x23
ENDC
    
SETUP:
    BANKSEL ANSEL	;DISABLE ANALOG INPUTS ON PORTA
    MOVLW H'00'
    MOVWF ANSEL
    BANKSEL PORTA	;CLEAR PORT A
    CLRF PORTA
    BANKSEL TRISA	;SET PORT A AS OUTPUTS
    MOVLW H'00'
    MOVWF TRISA
    
    BANKSEL ANSELH	;DISABLE ANALOG INPUTS ON PORTB
    MOVLW H'00'
    MOVWF ANSELH
    BANKSEL IOCB	;DISABLE INTERRUPT ON CHANGE
    MOVLW H'00'
    MOVWF IOCB
    BANKSEL WPUB	;DISABLE PORTB WEAK PULL-UPS
    MOVLW H'00'
    MOVWF WPUB
    BANKSEL PORTB	;CLEAR PORTB
    CLRF PORTB
    BANKSEL TRISB	;SET RB0..RB3 AS OUTPUTS, RB4..RB7 AS INPUTS
    MOVLW H'F0'
    MOVWF TRISB
    
    BANKSEL PORTB	;SELECT BANK 0
    GOTO MAIN
    
DELAY:			;DELAY SUBROUTINE WITH VARIABLE DELAY AMOUNT
    DECFSZ COUNTA
    GOTO DELAY
    MOVF STOREA, 0
    MOVWF COUNTA
    DECFSZ COUNTB
    GOTO DELAY
LOOP:
    DECFSZ COUNTC
    GOTO LOOP
    
    MOVLW H'01'		
    XORWF PORTA, 1	;TOGGLE RA0
    RETURN
    
NOPTABLE:		;A FINE ADJUSTMENT DELAY USING VARIABLE NOP IN TABLE
    ADDWF PCL		;ADD CURRENT W TO PCL TO CHANGE ADDRESS
    RETLW H'00'		;0x00 IN W, USED WHEN BUTTON IS NOT BEING PRESSED
    NOP			;Ox01 IN W
    NOP			;0x02 IN W
    RETURN		;OxO3 IN W
    
NOTETABLE:
    ADDWF PCL		;ADD THE CURRENT VALUE IN W TO PCL
    RETLW H'00'		;IF W IS 0, IMMEDIATELY RETURN
    MOVLW H'27'		;CONFIGURE COUNTERS FOR C3, TABLE ADDRESS 0x01
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'1F'
    MOVWF COUNTB
    MOVLW H'01'
    MOVWF COUNTC
    RETLW H'01'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'25'		;CONFIGURE COUNTERS FOR D3, TABLE ADDRESS 0x09
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'1D'
    MOVWF COUNTB
    MOVLW H'01'
    MOVWF COUNTC
    RETLW H'02'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'28'		;CONFIGURE COUNTERS FOR E3, TABLE ADDRESS 0x11
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'17'
    MOVWF COUNTB
    MOVLW H'26'
    MOVWF COUNTC
    RETLW H'01'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'27'		;CONFIGURE COUNTERS FOR F3, TABLE ADDRESS 0x19
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'17'
    MOVWF COUNTB
    MOVLW H'05'
    MOVWF COUNTC
    RETLW H'03'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'28'		;CONFIGURE COUNTERS FOR G3, TABLE ADDRESS 0x21
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'14'
    MOVWF COUNTB
    MOVLW H'01'
    MOVWF COUNTC
    RETLW H'01'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'29'		;CONFIGURE COUNTERS FOR A3, TABLE ADDRESS 0x29
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'11'
    MOVWF COUNTB
    MOVLW H'10'
    MOVWF COUNTC
    RETLW H'03'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'25'		;CONFIGURE COUNTERS FOR B3, TABLE ADDRESS 0x31
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'11'
    MOVWF COUNTB
    MOVLW H'01'
    MOVWF COUNTC
    RETLW H'02'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'25'		;CONFIGURE COUNTERS FOR C4, TABLE ADDRESS 0x39
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'10'
    MOVWF COUNTB
    MOVLW H'01'
    MOVWF COUNTC
    RETLW H'01'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'25'		;CONFIGURE COUNTERS FOR D4, TABLE ADDRESS 0x41
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'0E'
    MOVWF COUNTB
    MOVLW H'09'
    MOVWF COUNTC
    RETLW H'03'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'1B'		;CONFIGURE COUNTERS FOR E4, TABLE ADDRESS 0x49
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'11'
    MOVWF COUNTB
    MOVLW H'02'
    MOVWF COUNTC
    RETLW H'03'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'1B'		;CONFIGURE COUNTERS FOR F4, TABLE ADDRESS 0x51
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'10'
    MOVWF COUNTB
    MOVLW H'02'
    MOVWF COUNTC
    RETLW H'03'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'1B'		;CONFIGURE COUNTERS FOR G4, TABLE ADDRESS 0x59
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'0E'
    MOVWF COUNTB
    MOVLW H'06'
    MOVWF COUNTC
    RETLW H'01'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'1A'		;CONFIGURE COUNTERS FOR A4, TABLE ADDRESS 0x61
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'0D'
    MOVWF COUNTB
    MOVLW H'01'
    MOVWF COUNTC
    RETLW H'02'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'1B'		;CONFIGURE COUNTERS FOR B4, TABLE ADDRESS 0x69
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'0B'
    MOVWF COUNTB
    MOVLW H'03'
    MOVWF COUNTC
    RETLW H'01'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'1C'		;CONFIGURE COUNTERS FOR C5, TABLE ADDRESS 0x71
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'0A'
    MOVWF COUNTB
    MOVLW H'03'
    MOVWF COUNTC
    RETLW H'02'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    MOVLW H'18'		;CONFIGURE COUNTERS FOR D5, TABLE ADDRESS 0x79
    MOVWF COUNTA
    MOVWF STOREA
    MOVLW H'0A'
    MOVWF COUNTB
    MOVLW H'08'
    MOVWF COUNTC
    RETLW H'02'		;DETERMINE ANY ADDITIONAL DELAY USING NOPS
    
    
MAIN:
    CLRW		;CLEAR THE CURRENT W ADDRESS TO 0
    
    BSF PORTB, 0	;SET RB0 HIGH
    BTFSC PORTB, 4
    MOVLW H'01'		;SET W TO NOTETABLE ADDRESS FOR C3
    BTFSC PORTB, 5
    MOVLW H'09'		;SET W TO NOTETABLE ADDRESS FOR D3
    BTFSC PORTB, 6
    MOVLW H'11'		;SET W TO NOTETABLE ADDRESS FOR E3
    BTFSC PORTB, 7
    MOVLW H'19'		;SET W TO NOTETABLE ADDRESS FOR F3
    BCF PORTB, 0
    
    BSF PORTB, 1
    BTFSC PORTB, 4
    MOVLW H'21'		;SET W TO NOTETABLE ADDRESS FOR G3
    BTFSC PORTB, 5
    MOVLW H'29'		;SET W TO NOTETABLE ADDRESS FOR A3
    BTFSC PORTB, 6
    MOVLW H'31'		;SET W TO NOTETABLE ADDRESS FOR B3
    BTFSC PORTB, 7
    MOVLW H'39'		;SET W TO NOTETABLE ADDRESS FOR C4
    BCF PORTB, 1	;CLEAR RB1
    
    BSF PORTB, 2	;SET RB2 HIGH
    BTFSC PORTB, 4
    MOVLW H'41'		;SET W TO NOTETABLE ADDRESS FOR D4
    BTFSC PORTB, 5
    MOVLW H'49'		;SET W TO NOTETABLE ADDRESS FOR E4
    BTFSC PORTB, 6
    MOVLW H'51'		;SET W TO NOTETABLE ADDRESS FOR F4
    BTFSC PORTB, 7
    MOVLW H'59'		;SET W TO NOTETABLE ADDRESS FOR G4
    BCF PORTB, 2	;CLEAR RB2
    
    BSF PORTB, 3	;SET RB3 HIGH
    BTFSC PORTB, 4
    MOVLW H'61'		;SET W TO NOTETABLE ADDRESS FOR A4
    BTFSC PORTB, 5
    MOVLW H'69'		;SET W TO NOTETABLE ADDRESS FOR B4
    BTFSC PORTB, 6
    MOVLW H'71'		;SET W TO NOTETABLE ADDRESS FOR C5
    BTFSC PORTB, 7
    MOVLW H'79'		;SET W TO NOTETABLE ADDRESS FOR D5
    BCF PORTB, 3	;CLEAR RB3
    
    CALL NOTETABLE
    CALL NOPTABLE
    ADDLW H'00'		;USED TO SET/RESET THE ZERO FLAG
    BTFSS STATUS, 2
    CALL DELAY
    
    GOTO MAIN
    END